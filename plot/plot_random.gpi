#!/usr/bin/gnuplot
#
# $Id$
# $Author$
# $Date$
# $Rev: 34 $
#

reset

set terminal pdfcairo
set termoption dashed monochrome

set style line 11 lc rgb '#808080' lt 1
set border 3 back ls 11
set tics nomirror
set style line 12 lc rgb'#808080' lt 0 lw 1
set grid back ls 12

set style line 1 lc rgb '#8b1a0e' pt 4 ps 1.1 lt 1 lw 4.2
set style line 2 lc rgb '#5e9c36' pt 6 ps 1.1 lt 3 lw 4.2
set style line 3 lc rgb '#3e3c76' pt 12 ps 1.1 lt 5 lw 4.2
set style line 4 lc rgb '#9e7c26' pt 10 ps 1.1 lt 7 lw 4.2

bins = "mlton poly"
#bins = "mlton"
set key top left box ls 11 spacing 1.1
#set key outside box ls 11
set xlabel 'problem size'
set ylabel 'time needed (s)'
do for [bi = 1:words(bins)] {

  bin = word(bins, bi)

  set output 'out/time_'.bin.'_random_low.pdf'
  plot 'data/time_low_'.bin.'_balanced_6.csv' using 1:2 title 'max = 6' w lp ls 1, \
       'data/time_low_'.bin.'_balanced_5.csv' using 1:2 title 'max = 5' w lp ls 2, \
       'data/time_low_'.bin.'_balanced_4.csv' using 1:2 title 'max = 4' w lp ls 3

  set output 'out/time_'.bin.'_random_low_fast.pdf'
  plot 'data/time_low_'.bin.'_balanced_4.csv' using 1:2 title 'max = 4' w lp ls 1, \
       'data/time_low_'.bin.'_balanced_3.csv' using 1:2 title 'max = 3' w lp ls 2, \
       'data/time_low_'.bin.'_balanced_2.csv' using 1:2 title 'max = 2' w lp ls 3

  set output 'out/time_'.bin.'_random_hi.pdf'
  unset logscale y
  plot 'data/time_hi_'.bin.'_balanced_4.csv' using 1:2 title 'max = 4' w lp ls 1, \
       'data/time_hi_'.bin.'_balanced_3.csv' using 1:2 title 'max = 3' w lp ls 2, \
       'data/time_hi_'.bin.'_balanced_2.csv' using 1:2 title 'max = 2' w lp ls 3, \
       'data/time_hi_'.bin.'_pyramidal_.csv' using 1:2 title 'pyramidal' w lp ls 4

}
